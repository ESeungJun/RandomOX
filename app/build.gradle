apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.seungjun.randomox"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    useLibrary 'org.apache.http.legacy'

    flavorDimensions "randomox"
    defaultPublishConfig "Current"

    productFlavors {
        Current {
            dimension "randomox"
            versionCode getCurrentVersionCode()
            versionName getCurrentVersionName()
        }

        Previous {
            dimension "randomox"
            versionCode getPreVersionCode()
            versionName getPreVersionName()
        }
    }



    // signing에 필요한 키 정보들 저장
    signingConfigs {
        debug {
            storeFile file(keystore)
            storePassword keystore_pass
            keyAlias key_alias
            keyPassword key_pass
        }

        release {
            storeFile file(keystore)
            storePassword keystore_pass
            keyAlias key_alias
            keyPassword key_pass
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //GSON
    implementation 'com.google.code.gson:gson:2.8.5'

    //  네트워크 통신
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // 버터 나이프
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    implementation 'com.wang.avi:library:2.1.3'

    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'


}

apply plugin: 'com.google.gms.google-services'



//------------ 버전 설정 ----------------------
//현재버전 코드
def getCurrentVersionCode(){
    return 3;
}
//현재버전
def getCurrentVersionName(){
    return "1.2";
}
//업데이트용 코드
def getPreVersionCode(){
    return 0;
}
//업데이트용
def getPreVersionName(){
    return "0.0";
}
//------------ 버전 설정 ----------------------


//------------ 시간 설정 ----------------------
def getDateTime(){
    new Date().format('yyyyMMddHHmm')
}
//------------ 시간 설정 ----------------------

////////////[상용 구글 플레이]//////////////////
/**
 * 빌드 후 apk 위치 변경
 */
task renamePlayStoreApk(type: Copy){

    from 'build/outputs/apk/Current/release'
    into 'apk/'

    include('app-Current-release.apk')

    String apkName = getCurrentVersionName() +"-google-" + getDateTime()

    rename ('app-Current-release.apk', "RandomOX_"+apkName+".apk")

    delete "build/outputs/apk/Current/release/app-Current-release.apk"
}
////////////[상용 구글 플레이]//////////////////



////////////[상용 빌드]//////////////////
/**
 * 현재버전 구글플레이 상용 빌드
 */
task runBuildCurLivePlayStore(type: org.gradle.api.tasks.GradleBuild){
    tasks=['clean', 'assembleCurrentRelease', 'renamePlayStoreApk']
}

////////////[상용 빌드]//////////////////